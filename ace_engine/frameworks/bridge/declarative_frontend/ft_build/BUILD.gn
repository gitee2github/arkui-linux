# Copyright (c) 2023 Huawei Technologies Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/gn/fangtian.gni")
import("//ace_engine/ft_build/ace_config.gni")
import("//ace_engine/ft_build/build/ace_gen_obj.gni")

# declarative frontend source
template("declarative_frontend") {
  forward_variables_from(invoker, "*")

  ft_source_set(target_name) {
    defines += invoker.defines
    configs = [ "$ace_root/ft_build:ace_config" ]

    if (enable_ng_build) {
      sources = [
        "../ng/declarative_frontend_ng.cpp",
        "../ng/frontend_delegate_declarative_ng.cpp",
        "../ng/page_router_manager.cpp",
      ]
    } else {
      sources = [
        "../declarative_frontend.cpp",
        "../frontend_delegate_declarative.cpp",
        "../ng/page_router_manager.cpp",
      ]
    }

    deps = [ "$ace_root/frameworks/core/components/theme/ft_build:build_theme_code" ]
  }
}

foreach(item, ace_platforms) {
  declarative_frontend("declarative_frontend_" + item.name) {
    defines = []
    config = {
    }

    if (defined(item.config)) {
      config = item.config
    }

    if (defined(config.defines)) {
      defines = config.defines
    }
  }
}

# js resources objects
base_output_path = get_label_info(":gen_js_proxyclass", "target_out_dir")
jsproxyclass_obj_path = base_output_path + "/js_proxy_class.o"
jsenumstyle_obj_path = base_output_path + "/js_enum_style.o"
jsmocksystemplugin_obj_path = base_output_path + "/js_mock_system_plugin.o"

gen_obj("js_proxyclass") {
  input = "../engine/stateMgmt.js"
  if (use_linux) {
    jsproxyclass_obj_path = base_output_path + "/js_proxy_class.c"
  }
  output = jsproxyclass_obj_path
}

gen_obj("js_enum_style") {
  input = "../engine/jsEnumStyle.js"
  if (use_linux) {
    jsenumstyle_obj_path = base_output_path + "/js_enum_style.c"
  }
  output = jsenumstyle_obj_path
}

# build declarative javascript engine source
template("declarative_js_engine") {
  forward_variables_from(invoker, "*")

  ft_source_set(target_name) {
    defines += invoker.defines
    if (target_cpu == "arm64") {
      if (!is_mingw) {
        defines += [ "_ARM64_" ]
      }
    }
    if (current_os == "ohos" && current_cpu == "x86_64") {
      defines += [ "SIMULATOR_64" ]
    }

    deps = []
    public_deps = []
    configs = [ "$ace_root/ft_build:ace_config" ]

    sources = []

    if (defined(config.xcomponent_components_support) &&
        config.xcomponent_components_support) {
      sources += [
        "../jsview/js_xcomponent.cpp",
        "../jsview/js_xcomponent_controller.cpp",
        "../jsview/models/xcomponent_model_impl.cpp",
      ]
    }

    if (defined(config.enable_ability_component) &&
        config.enable_ability_component) {
      sources += [
        "../jsview/js_ability_component.cpp",
        "../jsview/js_ability_component_controller.cpp",
        "../jsview/models/ability_component_model_impl.cpp",
      ]
    }

    if (defined(config.web_components_support) &&
        config.web_components_support) {
      sources += [
        "../jsview/js_richtext.cpp",
        "../jsview/js_web.cpp",
        "../jsview/js_web_controller.cpp",
      ]
    }

    if (defined(config.form_components_support) &&
        config.form_components_support) {
      sources += [ "../jsview/js_form.cpp" ]
    }

    if (defined(config.remote_window_support) && config.remote_window_support) {
      sources += [ "../jsview/js_remote_window.cpp" ]
    }

    if (defined(config.plugin_components_support) &&
        config.plugin_components_support) {
      deps += [ "$ace_flutter_engine_root:third_party_flutter_engine_$platform" ]
      sources += [ "../jsview/js_plugin.cpp" ]
    }

    if (defined(config.preview_support) && config.preview_support) {
#sources += [ "jsview/js_previewer_mock.cpp" ]
    }

    if (defined(config.use_build_in_js_engine) &&
        config.use_build_in_js_engine) {
      deps += [ "$ace_root/frameworks/bridge:framework_bridge_$platform" ]
      defines += [ "BUILT_IN_JS_ENGINE" ]
    } else {
      sources += [
        "$ace_root/frameworks/bridge/js_frontend/engine/common/js_engine.cpp",
      ]
      deps += [ "$ace_root/ft_build/build:libace_static_${platform}" ]
      if (defined(config.build_container_scope_lib) &&
          config.build_container_scope_lib) {
        deps += [ "$ace_napi:ace_container_scope" ]
      }
    }

#deps += [ "engine/$engine_path:declarative_js_engine_bridge_${engine_name}_$platform" ]

    deps += [
      ":gen_obj_src_js_enum_style",
      ":gen_obj_src_js_proxyclass",
    ]

#   deps += uicast_configs.uicast_root_deps

    include_dirs = [
        # Fangtian TODO
        # "//foundation/window/window_manager/interfaces/innerkits", 
        "//arkcompiler/ets_runtime",
        "//arkcompiler/runtime_core"
    ]
#include_dirs += uicast_configs.uicast_root_include

    if (platform == "windows" || platform == "mac" || platform == "linux" || platform == "fangtian") {
#deps += [ ":gen_obj_src_js_mock_system_plugin" ]
    } else {
      if (defined(config.plugin_components_support) &&
          config.plugin_components_support) {
        if (!use_mingw_win && !use_mac && !use_linux) {
          external_deps = [
            "multimedia_image_framework:image",
            "multimedia_image_framework:image_native",
          ]
        }
      } else {
        external_deps = [
          "multimedia_image_framework:image",
          "multimedia_image_framework:image_native",
        ]
      }
    }
  }
}

# build ng declarative javascript engine source
template("declarative_js_engine_ng") {
  forward_variables_from(invoker, "*")

  ft_source_set(target_name) {
    defines += invoker.defines
    if (target_cpu == "arm64") {
      if (!is_mingw) {
        defines += [ "_ARM64_" ]
      }
    }
    if (current_os == "ohos" && current_cpu == "x86_64") {
      defines += [ "SIMULATOR_64" ]
    }

    deps = []
    configs = [ "$ace_root/ft_build:ace_config" ]

    sources = [
      "../engine/bindings_implementation.cpp",
      "../engine/declarative_engine_loader.cpp",
      "../engine/functions/js_animator_function.cpp",
      "../engine/functions/js_click_function.cpp",
      "../engine/functions/js_clipboard_function.cpp",
      "../engine/functions/js_drag_function.cpp",
      "../engine/functions/js_focus_function.cpp",
      "../engine/functions/js_foreach_function.cpp",
      "../engine/functions/js_function.cpp",
      "../engine/functions/js_gesture_function.cpp",
      "../engine/functions/js_hover_function.cpp",
      "../engine/functions/js_key_function.cpp",
      "../engine/functions/js_mouse_function.cpp",
      "../engine/functions/js_on_area_change_function.cpp",
      "../engine/functions/js_page_transition_function.cpp",
      "../engine/functions/js_pan_function.cpp",
      "../engine/functions/js_touch_function.cpp",
      "../engine/functions/js_webview_function.cpp",
      "../engine/js_types.cpp",
      "../jsview/js_environment.cpp",
      "../jsview/js_local_storage.cpp",
      "../jsview/js_persistent.cpp",
      "../jsview/js_view_functions.cpp",
      "../jsview/js_view_measure_layout.cpp",
      "../sharedata/js_share_data.cpp",

      # jsviews
      "../jsview/js_column.cpp",
      "../jsview/js_container_base.cpp",
      "../jsview/js_flex.cpp",
      "../jsview/js_grid_container.cpp",
      "../jsview/js_image.cpp",
      "../jsview/js_interactable_view.cpp",
      "../jsview/js_lazy_foreach.cpp",
      "../jsview/js_list.cpp",
      "../jsview/js_list_item.cpp",
      "../jsview/js_row.cpp",
      "../jsview/js_stack.cpp",
      "../jsview/js_text.cpp",
      "../jsview/js_view.cpp",
      "../jsview/js_view_abstract.cpp",
    ]

    sources += [
      "$ace_root/frameworks/bridge/js_frontend/engine/common/js_engine.cpp",
    ]

    if (defined(config.video_components_support) &&
        config.video_components_support) {
      sources += [ "../jsview/js_video.cpp" ]
    }

    if (defined(config.use_build_in_js_engine) &&
        config.use_build_in_js_engine) {
      deps += [ "$ace_root/frameworks/bridge:framework_bridge_ng_$platform" ]
      defines += [ "BUILT_IN_JS_ENGINE" ]
    } else {
      deps += [ "$ace_root/ft_build/build:libace_static_${platform}" ]
      if (defined(config.build_container_scope_lib) &&
          config.build_container_scope_lib) {
        deps += [ "$ace_napi:ace_container_scope" ]
      }
    }

    deps += [
      ":gen_obj_src_js_enum_style",
      ":gen_obj_src_js_proxyclass",
    ]
  }
}

# dynamic generate js_engine targets
foreach(item, ace_platforms) {
  platform = item.name
  engine_config = {
  }
  engine_config = item.config
  support_engines = []
  support_engines = engine_config.js_engines
  foreach(engine, support_engines) {
    declarative_js_engine(
        "declarative_js_engine_${engine.engine_name}_$platform") {
      platform = item.name
      defines = engine.engine_defines

      config = {}
      if (defined(item.config)) {
        config = item.config
      }
      if (defined(config.defines)) {
        defines += config.defines
      }
    }
  }
}
